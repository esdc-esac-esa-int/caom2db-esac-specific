plugins {
    id 'java'
    id 'maven'
    id 'maven-publish'
    id 'com.jfrog.bintray' version '1.7.3'
    id 'application'
    id 'checkstyle'
}

repositories {
    jcenter()
    mavenLocal()
}

sourceCompatibility = 1.8

group = 'org.opencadc'

version = '2.5'

mainClassName = 'esac.archive.ehst.dl.caom2.repo.client.publications.Main'

dependencies {
    compile 'log4j:log4j:1.2.+'
    compile 'net.sourceforge.javacsv:javacsv:2.+'
    compile 'javax.servlet:javax.servlet-api:3.1.+'
    
    compile 'org.opencadc:cadc-util:[1.0.14,)'
    compile 'org.opencadc:caom2:[2.3.5,3.0)'
    compile 'org.opencadc:caom2-persist:[2.3.2,3.0)'

    compile 'org.opencadc:cadc-registry:[1.2,)'
    compile 'org.opencadc:cadc-vosi:[1.0.1,2.0)'
	compile 'com.googlecode.json-simple:json-simple:[1.1,)'
    compile 'org.postgresql:postgresql:9.4.1209.jre7'
    compile 'com.mchange:c3p0:0.9.5.2'
    
    compile 'org.hibernate:hibernate-core:5.2.12.Final'    
    testCompile 'junit:junit:4.+'
    testCompile 'org.easymock:easymock:[3.2,4.0)'
}

// enable intTest 
configurations {
    intTestCompile.extendsFrom testCompile
    intTestRuntime.extendsFrom testRuntime
}

sourceSets {
    intTest {
        java {
            compileClasspath += main.output + test.output
            runtimeClasspath += main.output + test.output
            srcDir file('src/intTest/java')
        }
        
        // set the intTest resource directory
        resources.srcDir file('src/intTest/resources')
    }
}

tasks.withType(Test) {
    // reset the report destinations so that intTests go to their
    // own page
    //reports.html.destination = file("${reporting.baseDir}/${name}")
    reports.html.destination = file(reporting.baseDir.getAbsolutePath() + '/' + name)
    
    // Assign all Java system properties from
    // the command line to the tests
    systemProperties System.properties
}

task intTest(type: Test) {
    // set the configuration context
    testClassesDir = sourceSets.intTest.output.classesDir
    classpath = sourceSets.intTest.runtimeClasspath
    
    // run the tests always
    outputs.upToDateWhen { false }
}

apply from: '../opencadc.gradle'
